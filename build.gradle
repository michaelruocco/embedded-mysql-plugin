plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.gradle.plugin-publish' version '0.9.9'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '2.1.7'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.wix:wix-embedded-mysql:3.0.0'
    compile 'org.apache.commons:commons-lang3:3.7'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'mysql:mysql-connector-java:6.0.6'
    testCompile 'eu.codearte.catch-exception:catch-exception:1.4.6'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

pluginBundle {
    website = 'https://github.com/michaelruocco/embedded-mysql-plugin'
    vcsUrl = 'https://github.com/michaelruocco/embedded-mysql-plugin.git'
    description = 'Gradle plugin that enables running embedded MySQL for testing'
    tags = ['MySQL', 'embedded']
    version = project.version

    plugins {
        greetingsPlugin {
            id = 'com.github.michaelruocco.embedded-mysql-plugin'
            displayName = 'embedded-mysql-plugin'
        }
    }
}

signing {
    sign configurations.archives
}
signArchives.enabled = gradle.startParameter.taskNames.contains("uploadArchives")

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'Embedded MySQL Plugin'
                description 'Gradle plugin that enables running embedded MySQL for testing'
                groupId 'com.github.michaelruocco'
                artifactId 'embedded-mysql-plugin'
                version project.version
                url 'https://github.com/michaelruocco/embedded-mysql-plugin'

                developers {
                    developer {
                        id 'mruoc'
                        name 'Michael Ruocco'
                        email 'michael.ruocco@hotmail.com'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com/michaelruocco/embedded-mysql-plugin.git'
                    developerConnection 'scm:git:git@github.com/michaelruocco/embedded-mysql-plugin.git'
                    url 'git@github.com/michaelruocco/embedded-mysql-plugin.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}